let g:mapleader = ','
let g:maplocalleader = '\\'

call plug#begin('~/.vim/bundle')

if !has('nvim')
    Plug 'tpope/vim-sensible'
endif

Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': 'yes \| ./install --no-update-rc --no-completion --key-bindings' }
Plug 'rust-lang/rust.vim'
Plug 'nelstrom/vim-markdown-folding'
Plug 'tpope/vim-dispatch'
Plug 'junegunn/fzf.vim'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-vinegar'
Plug 'tpope/vim-unimpaired'
Plug 'junegunn/goyo.vim'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-eunuch'
Plug 'tpope/vim-markdown'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-rsi'
Plug 'fatih/vim-go'
Plug 'christoomey/vim-tmux-runner'
Plug 'elixir-editors/vim-elixir'
Plug 'mhinz/vim-mix-format'
Plug 'ElmCast/elm-vim'
Plug 'janko-m/vim-test'
Plug 'cespare/vim-toml'
Plug 'tikhomirov/vim-glsl'
Plug 'leafgarland/typescript-vim'
Plug 'kassio/neoterm'
Plug 'aklt/plantuml-syntax'
Plug 'tpope/vim-apathy'
Plug 'w0rp/ale'
Plug 'autozimu/LanguageClient-neovim', {
    \ 'branch': 'next',
    \ 'do': 'bash install.sh',
    \ }
Plug 'purescript-contrib/purescript-vim'
Plug 'reasonml-editor/vim-reason-plus'
Plug 'chriskempson/base16-vim'
Plug 'machakann/vim-highlightedyank'

call plug#end()

set mouse=a
set number
set relativenumber
set numberwidth=5
set ttimeoutlen=0
set shiftwidth=4
set tabstop=4
set smartcase
set expandtab
set ignorecase
set hidden
set tags+=.git/tags
set wildmode=list:longest,list:full
set noswapfile
set nobackup
set nowritebackup
set history=50
set complete=.,w,b,u,t
set ruler
set showcmd
set incsearch
set laststatus=2
set backspace=2
set autowrite
set nowrap
set breakindent
set synmaxcol=200
if has('nvim')
    set termguicolors
endif
" Use global regex replaces by default
set gdefault

" Center search results
nnoremap <silent> n nzz
nnoremap <silent> N Nzz
nnoremap <silent> * *zz
nnoremap <silent> # #zz
nnoremap <silent> g* g*zz

" Sane splits
set splitright
set splitbelow

if has('nvim')
    set inccommand=nosplit
endif

" Set up persistent undo
set undofile
set undodir=~/.vimundo

set modeline
set lazyredraw
if !has('nvim')
    set ttyfast
endif

function! InsertTabWrapper()
    let col = col('.') - 1
    if !col || getline('.')[col - 1] !~ '\k'
        return "\<tab>"
    else
        return "\<c-p>"
    endif
endfunction
inoremap <Tab> <c-r>=InsertTabWrapper()<cr>

nnoremap <space> :nohl<cr>
nnoremap <C-h> <C-w><C-h>
nnoremap <C-j> <C-w><C-j>
nnoremap <C-k> <C-w><C-k>
nnoremap <C-l> <C-w><C-l>
nnoremap cp :0,$y+<Cr>
nnoremap ' `
nnoremap ` '
nnoremap j gj
nnoremap k gk

" Magic search by default
nnoremap ? ?\v
nnoremap / /\v
cnoremap %s/ %sm/

" escape with Ctrl-j
inoremap <C-j> <esc>
vnoremap <C-j> <esc>
inoremap <C-c> <esc>
vnoremap <C-c> <esc>

" Reselect visual block after indent/outdent
vnoremap < <gv
vnoremap > >gv

if executable('rg')
    set grepprg=rg\ --vimgrep
endif

" FZF
set rtp+=~/.fzf
let g:fzf_command_prefix = 'Fzf'
nnoremap <leader>f :FzfGitFiles<cr>
nnoremap <leader>F :FzfFiles<cr>
nnoremap gb :FzfBuffers<cr>
nnoremap gs :FzfTags<cr>
nnoremap gl :FzfLines<cr>
nnoremap gL :FzfBLines<cr>
nnoremap <leader>a :FzfAg<cr>

" Configure vim tmux runner
let g:VtrStripLeadingWhitespace = 1
let g:VtrClearEmptyLines = 1
let g:VtrAppendNewline = 1

" Expand %% to the current directory
cnoremap %% <c-r>=expand('%:h') . '/'<cr>

nnoremap <silent> <leader>ev :tabe $MYVIMRC<Cr>

" Add mapping to open/close the quickfix list
" Taken from: http://vim.wikia.com/wiki/Toggle_to_open_or_close_the_quickfix_window
function! GetBufferList()
  redir =>buflist
  silent! ls!
  redir END
  return buflist
endfunction

function! ToggleList(bufname, pfx)
  let buflist = GetBufferList()
  for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
    if bufwinnr(bufnum) != -1
      exec(a:pfx.'close')
      return
    endif
  endfor
  if a:pfx == 'l' && len(getloclist(0)) == 0
      echohl ErrorMsg
      echo "Location List is Empty."
      return
  endif
  let winnr = winnr()
  exec(a:pfx.'open')
  if winnr() != winnr
    wincmd p
  endif
endfunction

" nmap <silent> <leader>l :call ToggleList("Location List", 'l')<CR>
nmap <silent> Q :call ToggleList("Quickfix List", 'c')<CR>

" Bind K to grep word under cursor
nnoremap <silent> K :grep! "<C-R><C-W>"<cr><cr>\|:copen<Cr>

" Default handler to run the current file
function! HandleEnter(cmd)
    if &buftype ==# 'quickfix' || &buftype ==# 'nofile'
        silent execute "normal! \<cr>"
    else
        silent execute "normal! :silent :update" . a:cmd . "\<CR>"
    endif
endfunction

" Default version where only the file is saved
nnoremap <silent> <cr> :call HandleEnter("")<cr>

" Switch panes
nnoremap <leader><leader> <C-^>

" remove help key
map <F1> <Esc>
imap <F1> <Esc>

" Configure vim test
nnoremap <silent> tf :update\|:TestFile<Cr>
nnoremap <silent> tl :update\|:TestLast<Cr>
nnoremap <silent> tc :update\|:TestNearest<Cr>
let test#python#runner = 'pytest'

nnoremap <leader>v :VtrAttachToPane<cr>

" Create shorter 'V' command which is easier to type
command! -nargs=? V call VtrSendCommand(<f-args>)

" Configure vim fugitive
nnoremap <leader>gc :Gcommit -v<cr>
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gw :Gwrite<cr>
nnoremap <leader>gr :Gread<cr>
nnoremap <leader>gs :Gstatus<cr>
nnoremap <leader>ga :Gcommit -v --amend<cr>

" Add mapping for ALENext/ALEPrevious
nmap <silent> [g <Plug>(ale_previous_wrap)
nmap <silent> ]g <Plug>(ale_next_wrap)
" Run the linter
nmap <silent> L <Plug>(ale_lint)

" Disable ALE colours as they get in the way
let g:ale_set_highlights = 0

" Configure ale
let g:ale_lint_on_text_changed = 'never'
let g:ale_lint_on_save = 0
let g:ale_lint_on_enter = 0
let g:ale_rust_cargo_use_check = 1
let g:ale_rust_cargo_check_all_targets = 1

" Configure language client
let g:LanguageClient_serverCommands = {
            \ 'rust': ['~/.0cargo/bin/rustup', 'run', 'stable', 'rls'],
            \ 'javascript': ['/usr/local/bin/javascript-typescript-stdio'],
            \ 'javascript.jsx': ['tcp://127.0.0.1:2089'],
            \ 'python': ['/usr/local/bin/pyls'],
            \ }

inoremap jj <Esc>

" When editing a file, always jump to the last known cursor position.
" Don't do it for commit messages, when the position is invalid, or when
" inside an event handler (happens when dropping a file on gvim).
autocmd BufReadPost *
\ if &ft != 'gitcommit' && line("'\"") > 0 && line("'\"") <= line("$") |
\   exe "normal g`\"" |
\ endif

" Configure neovim's terminal mode
if has('nvim')
	tnoremap <Esc> <C-\><C-n>
	tnoremap <M-[> <Esc>
	tnoremap <C-v><Esc> <Esc>

    autocmd TermOpen * startinsert
endif

" Automatically format elixir files on save
let g:mix_format_on_save = 1

if has('nvim')
    runtime! plugin/python_setup.vim
endif

set background=dark
colorscheme lucius
