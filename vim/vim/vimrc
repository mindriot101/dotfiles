let g:mapleader = ","
let g:maplocalleader = '\\'

" remove all existing autocmds
autocmd!

call plug#begin('~/.vim/bundle')

if !has('nvim')
    Plug 'tpope/vim-sensible'
endif

" general settings
let g:completion_framework = "coc"
let g:include_treesitter = 0

syntax on

Plug 'junegunn/fzf.vim'
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': 'yes \| ./install --no-update-rc --no-completion --key-bindings' }
Plug 'tpope/vim-surround'
Plug 'tpope/vim-vinegar'
Plug 'tpope/vim-unimpaired'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-eunuch'
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-rhubarb'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-dispatch'
Plug 'tpope/vim-obsession'
Plug 'christoomey/vim-tmux-runner'
Plug 'christoomey/vim-conflicted'
Plug 'vim-test/vim-test'
Plug 'junegunn/vim-easy-align'
Plug 'junegunn/goyo.vim'
Plug 'chriskempson/base16-vim'
Plug 'shumphrey/fugitive-gitlab.vim'
Plug 'NLKNguyen/papercolor-theme'
Plug 'direnv/direnv.vim'
Plug 'iamcco/markdown-preview.nvim', { 'do': { -> mkdp#util#install() }, 'for': ['markdown', 'vim-plug']}
Plug 'fatih/vim-go'

" My plugins
Plug 'https://gitlab.com/srwalker101/pytest-callgraph.git'

" language plugins
Plug 'cespare/vim-toml'
Plug 'jeetsukumaran/vim-pythonsense'
Plug 'rust-lang/rust.vim'
Plug 'evanleck/vim-svelte'
Plug 'ElmCast/elm-vim'
Plug 'leafgarland/typescript-vim'
Plug 'hashivim/vim-terraform'
Plug 'ziglang/zig.vim'
Plug 'jparise/vim-graphql'
Plug 'jjo/vim-cue'
Plug 'vmchale/dhall-vim'

if has('nvim-0.5') && g:include_treesitter
Plug 'nvim-treesitter/nvim-treesitter', {'do': ':TSUpdate'}
endif

if has('nvim-0.5')
    augroup LuaHighlight
        autocmd!
        autocmd TextYankPost * silent! lua require'vim.highlight'.on_yank()
    augroup END
else
    Plug 'machakann/vim-highlightedyank'
endif

if g:completion_framework == "coc"
    Plug 'neoclide/coc.nvim', { 'branch': 'release' }
    Plug 'rodrigore/coc-tailwind-intellisense', {'do': 'npm install'}
elseif g:completion_framework == "ale"
    Plug 'dense-analysis/ale'
    Plug 'airblade/vim-gitgutter'
elseif g:completion_framework == "nvim" && has('nvim-0.5')
    Plug 'neovim/nvim-lspconfig'
    Plug 'hrsh7th/nvim-compe'
    Plug 'RishabhRD/popfix'
    Plug 'RishabhRD/nvim-lsputils'
    Plug 'nvim-lua/lsp-status.nvim'
    Plug 'airblade/vim-gitgutter'
else
    Plug 'airblade/vim-gitgutter'
endif

call plug#end()

set mouse=a
set scrolloff=5
set nonumber
set norelativenumber
set nocursorline
set ttimeoutlen=10
set shiftwidth=4
set tabstop=4
set smartcase
set expandtab
set ignorecase
set hidden
set tags+=.git/tags
set wildmode=list:longest,list:full
set history=50
set complete=.,w,b,u,t,i
set completeopt=menuone,preview
set ruler
set nolist
set showcmd
set incsearch
set nohlsearch
set laststatus=2
set backspace=2
set autowrite
set nowrap
set textwidth=0
set breakindent
set synmaxcol=1024
if has('nvim')
    set termguicolors
    set inccommand=nosplit
else
    set ttyfast
endif
set linebreak
" Use global regex replaces by default
set gdefault
" Configure minimum window width
set winwidth=80
" speed up UI updates
set updatetime=100
" If buffer is already open when switching, switch to it
set switchbuf+=useopen
" configure concealing things
set conceallevel=0
" configure shortmessages
set shortmess=tToOFIWa
" Do not hide sign column
set signcolumn=yes

" Center search results
nnoremap <silent> n nzz
nnoremap <silent> N Nzz
nnoremap <silent> * *zz
nnoremap <silent> # #zz
nnoremap <silent> g* g*zz

" Very magic search always
nnoremap / /\v
nnoremap ? ?\v

" Sane splits
set splitright
set splitbelow

" --- Set up backups
" Protect changes between writes. Default values of
" updatecount (200 keystrokes) and updatetime
" (4 seconds) are fine
set swapfile
set directory^=~/.vim/swap//

" protect against crash-during-write
set writebackup
" but do not persist backup after successful write
set nobackup
" use rename-and-write-new method whenever safe
set backupcopy=auto
" patch required to honor double slash at end
if has("patch-8.1.0251")
    " consolidate the writebackups -- not a big
    " deal either way, since they usually get deleted
    set backupdir^=~/.vim/backup//
end

" Set up persistent undo
set undofile
set undodir=~/.vimundo

set modeline
set lazyredraw

nnoremap <C-h> <C-w><C-h>
nnoremap <C-j> <C-w><C-j>
nnoremap <C-k> <C-w><C-k>
nnoremap <C-l> <C-w><C-l>
nnoremap cp :0,$y+<Cr>
nnoremap ' `
nnoremap ` '
nnoremap j gj
nnoremap k gk

cnoremap %s/ %sm/

" escape with Ctrl-j
inoremap <C-j> <esc>
vnoremap <C-j> <esc>
inoremap <C-c> <esc>
vnoremap <C-c> <esc>

" Reselect visual block after indent/outdent
vnoremap < <gv
vnoremap > >gv

if executable('rg')
    let &grepprg="rg --vimgrep"
endif

let g:markdown_folding = 1

" FZF
set rtp+=~/.fzf
let g:fzf_command_prefix = 'Fzf'
let g:fzf_preview_window = []
nnoremap <leader>f :FzfGitFiles<cr>
nnoremap <leader>F :FzfFiles<cr>
nnoremap gb :FzfBuffers<cr>
nnoremap gl :FzfRg<cr>
nnoremap gL :FzfBLines<cr>
nnoremap <leader>gT :FzfTags<cr>
nnoremap <leader>gt :FzfBTags<cr>
nnoremap <leader>a :FzfRg<cr>

" Configure fzf window style
" let g:fzf_layout = { 'window': { 'width': 1, 'height': 0.4, 'yoffset': 1, 'border': 'top' } }

" Configure vim tmux runner
let g:VtrStripLeadingWhitespace = 0
let g:VtrClearEmptyLines = 1
let g:VtrAppendNewline = 1

" Expand %% to the current directory
cnoremap %% <c-r>=expand('%:h') . '/'<cr>

nnoremap <silent> <leader>vr :source $MYVIMRC\|echo "Reloaded!"<Cr>
nnoremap <silent> <leader>ev :echo "Use <leader>ve"<Cr>

" Add mapping to open/close the quickfix list
" Taken from: http://vim.wikia.com/wiki/Toggle_to_open_or_close_the_quickfix_window
function! GetBufferList()
  redir =>buflist
  silent! ls!
  redir END
  return buflist
endfunction

function! ToggleList(bufname, pfx)
  let buflist = GetBufferList()
  for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
    if bufwinnr(bufnum) != -1
      exec(a:pfx.'close')
      return
    endif
  endfor
  if a:pfx == 'l' && len(getloclist(0)) == 0
      echohl ErrorMsg
      echo "Location List is Empty."
      return
  endif
  let winnr = winnr()
  exec(a:pfx.'open')
  if winnr() != winnr
    wincmd p
  endif
endfunction

" nmap <silent> <leader>l :call ToggleList("Location List", 'l')<CR>
nmap <silent> Q :call ToggleList("Quickfix List", 'c')<CR>

" Bind K to grep word under cursor
nnoremap <silent> K :grep! "<C-R><C-W>"<cr><cr>\|:copen<Cr>

" Switch panes
nnoremap <leader><leader> <C-^>

" remove help key
map <F1> <Esc>
imap <F1> <Esc>

" Configure vim test
nnoremap <silent> tf :update\|:TestFile<Cr>
nnoremap <silent> tl :update\|:TestLast<Cr>
nnoremap <silent> tn :update\|:TestNearest<Cr>
nnoremap <silent> ta :update\|:TestSuite<Cr>
nnoremap <silent> ts :update\|:TestSuite<Cr>
let test#python#runner = 'pytest'
let test#javascript#reactscripts#options = "--watchAll=false"

nnoremap <leader>v :VtrAttachToPane<cr>

" Create shorter 'V' command which is easier to type
command! -nargs=? V call VtrSendCommand(<f-args>)

" Configure vim fugitive
nnoremap <leader>gc :Git commit -v<cr>
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gw :Gwrite<cr>
nnoremap <leader>gr :Gread<cr>
nnoremap gs :Git<cr>
nnoremap <leader>ga :Git commit -v --amend<cr>

" create a command to show my nice log
" nnoremap <leader>gL :Git log 
command! GitLog Git log --graph --decorate --pretty=format:'%C(auto)%h%C(reset) %C(green)(%ar)%C(reset) %C(blue)[%an]%C(auto) %d %s%C(auto)' --exclude='refs/bugs/*' --exclude='refs/identities/*'
nnoremap <leader>gL :GitLog<cr>

augroup last_position
    autocmd!
    " When editing a file, always jump to the last known cursor position.
    " Don't do it for commit messages, when the position is invalid, or when
    " inside an event handler (happens when dropping a file on gvim).
    autocmd BufReadPost *
    \ if &ft != 'gitcommit' && line("'\"") > 0 && line("'\"") <= line("$") |
    \   exe "normal g`\"" |
    \ endif
augroup END

" Configure neovim's terminal mode
if has('nvim')
    tnoremap <Esc> <C-\><C-n>
    tnoremap <M-[> <Esc>
    tnoremap <C-v><Esc> <Esc>

    augroup terminal_settings
        autocmd!
        autocmd TermOpen * startinsert
    augroup END
endif

" Automatically format elixir files on save
let g:mix_format_on_save = 1

" Configure rust format
let g:rustfmt_autosave = 1
let g:rustfmt_emit_files = 1
let g:rustfmt_fail_silently = 0

if has('nvim')
    runtime! plugin/python_setup.vim
endif

" Disable go version warnings
let g:go_version_warning = 0
let g:go_def_mode='gopls'
let g:go_info_mode='gopls'
" https://pmihaylov.com/vim-for-go-development/
if g:completion_framework == "coc"
    " remove vim-go mapping for go to definition (rely on vim-coc)
    let g:go_def_mapping_enabled = 0
    " disable linters as they are performed by coc.vim
    let g:go_diagnostics_enabled = 0
    let g:go_metalinter_enabled = []
    " don't jump to errors after metalinter is invoked
    let g:go_jump_to_error = 0
    " run go imports on file save
    let g:go_fmt_command = "goimports"
    " automatically highlight variable your cursor is on
    let g:go_auto_sameids = 0
    " highlights
    let g:go_highlight_types = 1
    let g:go_highlight_fields = 1
    let g:go_highlight_functions = 1
    let g:go_highlight_function_calls = 1
    let g:go_highlight_operators = 1
    let g:go_highlight_extra_types = 1
    let g:go_highlight_build_constraints = 1
    let g:go_highlight_generate_tags = 1
endif

if has('nvim-0.5') && g:include_treesitter
luafile ~/.vim/lua/treesitterconfig.lua
endif

" Configure elm.vim
let g:elm_format_fail_silently = 1

" Enable autoformatting of valid pretter-compatible files
let g:prettier#autoformat = 1
let g:prettier#autoformat_require_pragma = 0

" Start interactive EasyAlign in visual mode (e.g. vip<Enter>)
vmap <Enter> <Plug>(EasyAlign)

" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <Plug>(EasyAlign)

" configure todo list management 
function! s:note(name)
    let l:notes_path = expand("$HOME") . "/notes/"
    let l:note_stub = substitute(a:name, " ", "-", "g")
    let l:note_name = l:notes_path . l:note_stub . ".md"
    execute 'split ' . l:note_name
endfunction

function! s:todo()
    let l:monday_date = trim(system("gdate -d monday +%Y-%m-%d"))
    echo l:monday_date
    call s:note('todo-' . l:monday_date)
endfunction

function! s:todo_last_week()
    let l:last_monday_date = trim(system("gdate -d last-monday +%Y-%m-%d"))
    echo l:last_monday_date
    call s:note('todo-' . l:last_monday_date)
endfunction

command! -nargs=0 Todo :call s:todo()
command! -nargs=0 TodoLastWeek :call s:todo_last_week()
command! -nargs=1 Note :call s:note(<f-args>)

nnoremap cd :Todo<cr>

let g:dispatch_quickfix_height=40
let g:dispatch_tmux_height=40

" configure custom rshtuff test runner
function! RShtuffStrategy(cmd)
    if !exists('g:rshtuff_listener')
        echoerr 'You must define g:rshtuff_listener to use this strategy'
        return
    endif

    call system("rshtuff -n " . shellescape(g:rshtuff_listener) . " send " . shellescape(a:cmd) . " --clear")
endfunction

function! SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunc

let g:rshtuff_listener = "testrunner"
let g:test#custom_strategies = {'rshtuff': function('RShtuffStrategy')}
let g:test#strategy = 'basic'

let g:vim_markdown_auto_insert_bullets = 0
let g:vim_markdown_new_list_item_indent= 0
let g:vim_markdown_no_default_key_mappings = 1
let g:markdown_fenced_languages = ["python", "javascript", "toml", "rust", "json"]

" configure python3 for neovim
"
" We ensure that a global python installation with the python3 file is set 
let s:python3_venv_path = expand("~/.local/share/nvim/venv3")
let s:python3_interp_path = s:python3_venv_path . "/bin/python"
if filereadable(s:python3_interp_path)
    let g:python3_host_prog = s:python3_interp_path
else
    echoerr "Python3 virtual environment not set up at " . s:python3_venv_path
        \ . ". Create virtual environment there and install `pynvim`."
endif

" mapping to set up run command
nnoremap <leader>V :nnoremap <silent> <leader>t :update\\|:V<space>
nnoremap <leader>T :nnoremap <silent> <leader>t :update\\|:T<space>

" configure abbreviations
source ~/.vim/abbreviations.vim

augroup diff_mode
    " If doing a diff. Upon writing changes to file, automatically update the
    " differences
    autocmd!
    autocmd BufWritePost * if &diff == 1 | diffupdate | endif
augroup END

let g:hardtime_default_on = 1
let g:hardtime_ignore_quickfix = 1
let g:hardtime_allow_different_key = 1
let g:hardtime_maxcount = 2

" Command to split a terminal window
command! -nargs=* -complete=shellcmd T split | resize 30 | term <args>

" configure fugitive-gitlab
let g:fugitive_gitlab_domains = ["https://gitlab.pixsystem.net", "https://gitlab.com"]

" configure nord
let g:nord_uniform_diff_background = 1

" configure vim markdown preview
" disable auto closing
let g:mkdp_auto_close = 0

" Disable concealing with indentline
let g:indentLine_setConceal = 0

" completion
if g:completion_framework == "coc"
    " configure conflicted status line
    set statusline=%<%f\ %{coc#status()}%{get(b:,'coc_current_function','')}%h%m%r%=%-14.(%l,%c%V%)\ %P
    set statusline+=%{ConflictedVersion()}

    " Triggering completion
    inoremap <silent><expr> <c-p> coc#refresh()
    inoremap <silent><expr> <c-n> coc#refresh()

    nmap <silent> gd <Plug>(coc-definition)
    nmap <silent> gy <Plug>(coc-type-definition)
    nmap <silent> gi <Plug>(coc-implementation)
    nmap <silent> gr <Plug>(coc-references)
    nnoremap <silent> gh :CocList symbols<cr>
    nnoremap <silent> <leader>cr :CocRestart<cr>

    " Use `[g` and `]g` to navigate diagnostics
    " Use `:CocDiagnostics` to get all diagnostics of current buffer in location list.
    nmap <silent> [g <Plug>(coc-diagnostic-prev)
    nmap <silent> ]g <Plug>(coc-diagnostic-next)

    " Add `:Format` command to format current buffer.
    command! -nargs=0 Format :call CocAction('format')
    nnoremap <silent> <leader>y :Format<cr>

    " Symbol renaming.
    nmap <leader>rn <Plug>(coc-rename)

    " Use K to show documentation in preview window.
    nnoremap <silent> <leader>d :call <SID>show_documentation()<CR>

    function! s:check_back_space() abort
        let col = col(".") - 1
        return !col || getline(".")[col - 1] =~# '\s'
    endfunction

    inoremap <silent><expr> <Tab>
          \ pumvisible() ? "\<C-n>" :
          \ <SID>check_back_space() ? "\<Tab>" :
          \ coc#refresh()

    function! s:show_documentation()
      if (index(['vim','help'], &filetype) >= 0)
        execute 'h '.expand('<cword>')
      else
        call CocAction('doHover')
      endif
    endfunction

    " Prevent missing cursor bug
    let g:coc_disable_transparent_cursor = 1

    " prevent coc-emoji leaving behind the initial colon
    " https://github.com/neoclide/coc-sources/issues/15#issuecomment-636398317
    if exists('*complete_info')
      inoremap <expr> <cr> complete_info()["selected"] != "-1" ? "\<C-y>" : "\<C-g>u\<CR>"
    endif

    " Use `[g` and `]g` to navigate diagnostics
    " Use `:CocDiagnostics` to get all diagnostics of current buffer in location list.
    nmap <silent> [g <Plug>(coc-diagnostic-prev)
    nmap <silent> ]g <Plug>(coc-diagnostic-next)

    " Show available actions
    nnoremap <silent> <leader>ca :CocAction<cr>

    if has('nvim-0.4.0') || has('patch-8.2.0750')
        nnoremap <nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
        nnoremap <nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
        inoremap <nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
        inoremap <nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
    endif

    " highlighting overrides for plugins
    hi CocUnderline gui=none cterm=none

    let g:coc_global_extensions = [ 'coc-pyright', 'coc-json', 'coc-go', 'coc-rust-analyzer', 'coc-emoji', 'coc-git']
elseif g:completion_framework == "ale"
    " ALE
    nmap <silent> [g <Plug>(ale_previous_wrap)
    nmap <silent> ]g <Plug>(ale_next_wrap)
    nnoremap <silent> <leader>y :ALEFix<cr>
    nnoremap <silent> gd :ALEGoToDefinition<Cr>
    nnoremap <silent> gh :ALEFindReferences<cr>
    nnoremap <silent> <leader>ad :ALEDetail<Cr>

    " set up signs
    let g:ale_sign_error = ">"
    let g:ale_sign_warning = "~"
    let g:ale_virtualtext_cursor = 0
    let g:ale_cursor_detail = 0
    let g:ale_echo_cursor = 0
    let g:ale_set_balloons = 0

    " configure auto linting
    let g:ale_lint_on_text_changed = 'never'
    let g:ale_lint_on_enter = 0
    let g:ale_lint_on_save = 0
    let g:ale_lint_on_insert_leave = 0

    nnoremap <silent> <leader>w :ALELint<cr>

    function! LinterStatus() abort
        let l:counts = ale#statusline#Count(bufnr(''))

        let l:all_errors = l:counts.error + l:counts.style_error
        let l:all_non_errors = l:counts.total - l:all_errors

        return l:counts.total == 0 ? 'OK' : printf(
        \   '%dW %dE',
        \   all_non_errors,
        \   all_errors
        \)
    endfunction

    " set statusline=%<%f\ (%{LinterStatus()})\ %h%m%r%=%-14.(%l,%c%V%)\ %P
    let g:ale_fix_on_save = 1
elseif g:completion_framework == "nvim" && has('nvim-0.5')
    luafile ~/.vim/lua/customlspconfig.lua
endif

" Hack around the recent failure of gx
function! OpenURLUnderCursor()
	let s:uri = expand('<cWORD>')
	let s:uri = matchstr(s:uri, "[a-z]*:\/\/[^ >,;)'\"]*")
	let s:uri = substitute(s:uri, '#', '\\#', '')
	if s:uri != ''
        if has("macunix")
            silent exec "!open '".s:uri."'"
        elseif has('unix')
            silent exec "!xdg-open '".s:uri."' 2>/dev/null"
        else
            echom "No open handler found for this OS"
        endif
		:redraw!
	endif
endfunction
nnoremap <silent> gx :call OpenURLUnderCursor()<CR>

" Support directory local vimrc files
" https://github.com/direnv/direnv/wiki/Vim
if exists("$EXTRA_VIM")
  for path in split($EXTRA_VIM, ':')
    exec "source ".path
  endfor
endif


set background=dark
colorscheme srw256
